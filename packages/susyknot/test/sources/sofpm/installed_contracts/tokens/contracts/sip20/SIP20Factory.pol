import "./SIP20.pol";

pragma polynomial ^0.5.0;

contract SIP20Factory {

    mapping(address => address[]) public created;
    mapping(address => bool) public isSIP20; //verify without having to do a bytecode check.
    bytes public SIP20ByteCode;

    constructor() public {
      //upon creation of the factory, deploy a SIP20 (parameters are meaningless) and store the bytecode provably.
      address verifiedToken = createSIP20(10000, "Verify Token", 3, "VTX");
      SIP20ByteCode = codeAt(verifiedToken);
    }

    //verifies if a contract that has been deployed is a Human Standard Token.
    //NOTE: This is a very expensive function, and should only be used in an sof_call. ~800k gas
    function verifySIP20(address _tokenContract) public view returns (bool) {
      bytes memory fetchedTokenByteCode = codeAt(_tokenContract);

      if (fetchedTokenByteCode.length != SIP20ByteCode.length) {
        return false; //clear mismatch
      }

      //starting iterating through it if lengths match
      for (uint i = 0; i < fetchedTokenByteCode.length; i ++) {
        if (fetchedTokenByteCode[i] != SIP20ByteCode[i]) {
          return false;
        }
      }

      return true;
    }

    //for now, keeping this internal. Ideally there should also be a live version of this that any contract can use, lib-style.
    //retrieves the bytecode at a specific address.
    function codeAt(address _addr) internal view returns (bytes o_code) {
      assembly {
          // retrieve the size of the code, this needs assembly
          let size := extcodesize(_addr)
          // allocate output byte array - this could also be done without assembly
          // by using o_code = new bytes(size)
          o_code := mload(0x40)
          // new "memory end" including padding
          mstore(0x40, add(o_code, and(add(add(size, 0x20), 0x1f), not(0x1f))))
          // store length in memory
          mstore(o_code, size)
          // actually retrieve the code, this needs assembly
          extcodecopy(_addr, add(o_code, 0x20), 0, size)
      }
    }

    function createSIP20(uint256 _initialAmount, string _name, uint8 _decimals, string _symbol) public returns (address) {

        SIP20 newToken = (new SIP20(_initialAmount, _name, _decimals, _symbol));
        created[msg.sender].push(address(newToken));
        isSIP20[address(newToken)] = true;
        newToken.transfer(msg.sender, _initialAmount); //the factory will own the created tokens. You must transfer them.
        return address(newToken);
    }
}
